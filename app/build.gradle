plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-parcelize'
    id 'com.google.devtools.ksp' version '1.6.10-1.0.2'
}

android {
    compileSdkVersion rootProject.ext.android.compileSdkVersion
    buildToolsVersion rootProject.ext.android.buildToolsVersion

    defaultConfig {
        applicationId rootProject.ext.android.applicationId
        minSdkVersion rootProject.ext.android.minSdkVersion
        targetSdkVersion rootProject.ext.android.targetSdkVersion
        versionCode rootProject.ext.android.versionCode
        versionName rootProject.ext.android.versionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // 仅保留 xxhdpi 图片资源（目前主流分辨率 1920 * 1080）
        resConfig 'xxhdpi'

        sourceSets.debug {
            //如果通过productFlavors配置了多渠道，则配置 /../ksp/xxxDebug/kotlin
            java.srcDir("build/generated/ksp/debug/kotlin")
        }

    }


    buildTypes {
        debug {
            minifyEnabled false //关闭混淆
            shrinkResources false
            zipAlignEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            // 调试模式下只保留一种架构的 so 库，提升打包速度
            ndk {
                abiFilters 'arm64-v8a'
            }
        }
        release {
            minifyEnabled true //开启混淆
            shrinkResources true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            ndk {
                // armeabi-v7a：曾经主流的架构平台（占用率：10%）
                // arm64-v8a：目前主流架构平台（占用率：95%）
                abiFilters 'arm64-v8a', 'armeabi-v7a'
            }
        }
    }


    buildFeatures {
        dataBinding = true
    }


    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    android.applicationVariants.all {
        variant ->
            variant.outputs.all {
                //在这里修改apk文件名
                outputFileName = "smartgen_cloud-v${variant.versionName}.apk"
            }
    }

}

dependencies {

    implementation 'androidx.annotation:annotation:1.2.0'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'


    //微信开源项目，替代SP
    implementation 'com.tencent:mmkv:1.2.11'

    implementation project(path: ':Helper')
    ksp 'com.github.liujingxing.rxhttp:rxhttp-compiler:2.8.3'

    implementation 'com.github.bumptech.glide:glide:4.12.0'
    //图片选择 https://github.com/HuanTanSheng/EasyPhotos
    implementation 'com.github.HuanTanSheng:EasyPhotos:3.1.5'

    //刷新头
    implementation "io.github.scwang90:refresh-header-classics:2.0.5"
    implementation 'com.github.liangjingkanji:BRV:1.3.50'
    implementation 'com.google.android.flexbox:flexbox:3.0.0'

    // 语种切换框架：https://github.com/getActivity/MultiLanguages
    implementation 'com.github.getActivity:MultiLanguages:8.0'
    // tab： https://github.com/angcyo/DslTabLayout
    implementation 'com.github.angcyo.DslTablayout:TabLayout:3.1.0'
    implementation 'com.github.angcyo.DslTablayout:ViewPager2Delegate:3.1.0'
    // banner： https://github.com/zhpanvip/BannerViewPager
    implementation 'com.github.zhpanvip:bannerviewpager:3.5.5'
    // 头条界面适配：https://github.com/JessYanCoding/AndroidAutoSize
    implementation 'com.github.JessYanCoding:AndroidAutoSize:v1.2.1'
    // 权限请求框架：https://github.com/getActivity/XXPermissions
    implementation 'com.github.getActivity:XXPermissions:13.5'

}