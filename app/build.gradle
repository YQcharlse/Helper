plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-parcelize'
    id 'com.google.devtools.ksp' version '1.8.10-1.0.9'
}

android {
    compileSdkVersion rootProject.ext.android.compileSdkVersion
    buildToolsVersion rootProject.ext.android.buildToolsVersion

    defaultConfig {
        applicationId rootProject.ext.android.applicationId
        minSdkVersion rootProject.ext.android.minSdkVersion
        targetSdkVersion rootProject.ext.android.targetSdkVersion
        versionCode rootProject.ext.android.versionCode
        versionName rootProject.ext.android.versionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // 仅保留 xxhdpi 图片资源（目前主流分辨率 1920 * 1080）
        resConfig 'xxhdpi'

        sourceSets.debug {
            //如果通过productFlavors配置了多渠道，则配置 /../ksp/xxxDebug/kotlin
            java.srcDir("build/generated/ksp/debug/kotlin")
        }

    }


    buildTypes {
        debug {
            minifyEnabled false //关闭混淆
            shrinkResources false
            zipAlignEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            // 调试模式下只保留一种架构的 so 库，提升打包速度
            ndk {
                abiFilters 'arm64-v8a'
            }
        }
        release {
            minifyEnabled true //开启混淆
            shrinkResources true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            ndk {
                // armeabi-v7a：曾经主流的架构平台（占用率：10%）
                // arm64-v8a：目前主流架构平台（占用率：95%）
                abiFilters 'arm64-v8a', 'armeabi-v7a'
            }
        }
    }


    buildFeatures {
        //noinspection DataBindingWithoutKapt
        dataBinding = true
    }

    android.applicationVariants.all {
        variant ->
            variant.outputs.all {
                //在这里修改apk文件名
                outputFileName = "smartgen_cloud-v${variant.versionName}.apk"
            }
    }

}

dependencies {

    //微信开源项目，替代SP
    implementation 'com.tencent:mmkv:1.2.15'

    implementation project(path: ':Helper')
    ksp 'com.github.liujingxing.rxhttp:rxhttp-compiler:3.2.4'

    implementation 'com.github.bumptech.glide:glide:4.15.1'

    //刷新头
    implementation "io.github.scwang90:refresh-header-classics:2.0.5"
    implementation 'com.github.liangjingkanji:BRV:1.4.1'
    implementation 'com.google.android.flexbox:flexbox:3.0.0'

    // 语种切换框架：https://github.com/getActivity/MultiLanguages
    implementation 'com.github.getActivity:MultiLanguages:8.0'
    // tab： https://github.com/angcyo/DslTabLayout
    implementation 'com.github.angcyo.DslTablayout:TabLayout:3.3.1'
    implementation 'com.github.angcyo.DslTablayout:ViewPager2Delegate:3.3.1'
    // banner： https://github.com/zhpanvip/BannerViewPager
    implementation 'com.github.zhpanvip:bannerviewpager:3.5.6'
    // 头条界面适配：https://github.com/JessYanCoding/AndroidAutoSize
    implementation 'com.github.JessYanCoding:AndroidAutoSize:v1.2.1'
    // 权限请求框架：https://github.com/getActivity/XXPermissions
    implementation 'com.github.getActivity:XXPermissions:16.2'

    //XPoup 弹窗  https://github.com/li-xiaojun/XPopup
    api 'com.github.li-xiaojun:XPopup:2.9.13'

}